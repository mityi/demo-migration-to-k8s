plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.1.2.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.6.RELEASE' apply false
    id 'io.franzbecker.gradle-lombok' version '2.0' apply false
    id 'com.palantir.docker' version '0.21.0' apply false
}
allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'java'
    apply plugin: 'idea'
}
subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'com.palantir.docker'

    group = 'ru.fxf'
    version = '0.0.2'
    sourceCompatibility = JavaVersion.VERSION_1_8

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE'
        }
        dependencies {
            dependency "javax.xml.bind:jaxb-api:2.2.11"
            dependency "com.sun.xml.bind:jaxb-core:2.2.11"
            dependency "com.sun.xml.bind:jaxb-impl:2.2.11"
            dependency "javax.activation:activation:1.1.1"
        }
    }

    jar {
        baseName = project.name
    }

    docker {
        name "hub.docker.com/mityi/demo-service-discovery-${jar.baseName}:${version}"
        files jar.archivePath
        buildArgs(['JAR_FILE': "${jar.archiveName}"])
        dockerfile = file("../docker/Dockerfile")
    }
}
